defmodule VideoCommentator.Multimedia.Video do
  use Ecto.Schema
  import Ecto.Changeset

  @primary_key {:id, VideoCommentator.Multimedia.Permalink, autogenerate: true}
  schema "videos" do
    field :description, :string
    field :title, :string
    field :url, :string
    field :slug, :string

    belongs_to :user, VideoCommentator.Accounts.User
    belongs_to :category, VideoCommentator.Multimedia.Category
    has_many :annotations, VideoCommentator.Multimedia.Annotation

    timestamps()
  end

  @doc false
  def changeset(video, attrs) do
    video
    |> cast(attrs, [:url, :title, :description, :category_id])
    |> validate_required([:url, :title, :description])
    # |> validate_required([:url, :title, :description, :category_id])
    # |> assoc_constraint(:category)
    |> slugify_title()
  end

  defp slugify_title(changeset) do
    case fetch_change(changeset, :title) do
      {:ok, new_title} -> put_change(changeset, :slug, slugify(new_title))
      :error -> changeset
    end
  end

  defp slugify(str) do
    str
    |> String.downcase()
    |> String.replace(~r/[^\w-]+/u, "-")
  end
end
